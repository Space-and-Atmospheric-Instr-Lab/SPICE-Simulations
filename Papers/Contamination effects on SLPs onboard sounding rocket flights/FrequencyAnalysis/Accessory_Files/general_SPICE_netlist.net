;Comment to Initialize
; Define constants 
.PARAM Qe = 1.602e-19
.PARAM Kb = 1.381e-23
.PARAM Pix = 3.1415926
.PARAM Me = 9.109e-31
.PARAM Mi = 1.673e-27
.PARAM Mion  = 16
.PARAM Te  = 1000
.PARAM Ti  = Te
.PARAM N   = 500000000000
.PARAM res = 50000
.PARAM cap = 1e-06
; Payload Surface
Xsurface 4 0 OMLEquation PARAMS: A=100.1493, ebeta=0.5,ionbeta = 0.5
; Voltage Applied to SLP
Vdac 1 4 PWL(0, -5,5, 5,10, -5,10, -5,15, 5,20, -5,25, 5,25, 5,)
;SLP Contamination Layer
R 1 3 {res}
C 1 3 {cap}
; Sweeping Langmuir Probe
Xlangmuir 3 0 OMLEquation PARAMS: A=0.00052339, ebeta = 0.8,ionbeta = 0.8
.IC V(1) = -5.3715 V(3) = -5.3552 V(4) = -0.37149
.OP
.TRAN 1e-7 11 0 1e-06 
.subckt OMLEquation a b PARAMS: Ne={N}, Ni={N}, M={Mion},ebeta = 0.5,ionbeta = 0.5
Gi a b VALUE = { IF ( V(a) <= V(b),   -Qe * Ni * A * sqrt( (Kb * Ti / (2 * Pix * Mi * M))) * (1 - (Qe * V(a) / (Kb * Ti)))^ionbeta,
+ -Qe * Ni * A * sqrt( Kb * Te / (2 * Pi * Mi * M)) * exp( -Qe * V(a) / (Kb * Ti)))}
Ge a b VALUE = { IF ( V(a) >= V(b),  Qe * Ne * A * sqrt( (Kb * Te / (2 * Pix * Me))) * (1 + (Qe * V(a) / (Kb * Te)))^ebeta,
+  Qe * Ne * A * sqrt( Kb * Te / (2 * Pi * Me)) * exp( Qe * V(a) / (Kb * Te)))}
R a b 10000000T
.ends
.END
