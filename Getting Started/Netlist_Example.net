;Comment to Initialize

;Define Constants
.PARAM Qe = 1.602e-19
.PARAM Kb = 1.381e-23
.PARAM Pix = 3.1415926
.PARAM Me = 9.109e-31
.PARAM Mi = 1.673e-27
.PARAM Mion  = 16
.PARAM Te  = 1000
.PARAM Ti  = Te
.PARAM N   = 500000000000
.PARAM res = 50000
.PARAM cap = 1e-06

; Current to Payload Surface (Voltage Dependent Current Source)
Xsurface 4 0 OMLEquation PARAMS: A=100.1493, ebeta=0.5,ionbeta = 0.5

; Voltage Applied to SLP (triangular waveform, -5V to +5V, 10Hz)
Vdac 1 4 PWL(0, -5,0.05, 5,0.1, -5,0.1, -5,0.15, 5,0.2, -5,0.25, 5,0.25, 5)

; Contamination Layer on SLP
R 1 3 {res}
C 1 3 {cap}

; Current to SLP (Voltage Dependent Current Source)
Xlangmuir 3 0 OMLEquation PARAMS: A=0.00052339, ebeta = 0.8,ionbeta = 0.8

.OP

; Transient Analysis (step = 1e-7, simulation duration = 0.2 seconds, time start = 0, max time step = 1e-5s)
.TRAN 1e-7 0.2 0 1e-05

; Define Voltage Dependent Current Source (Orbital Motion Limited Theory)
.subckt OMLEquation a b PARAMS: Ne={N}, Ni={N}, M={Mion},ebeta = 0.5,ionbeta = 0.5
Gi a b VALUE = { IF ( V(a) <= V(b),   -Qe * Ni * A * sqrt( (Kb * Ti / (2 * Pix * Mi * M))) * (1 - (Qe * V(a) / (Kb * Ti)))^ionbeta,
+ -Qe * Ni * A * sqrt( Kb * Te / (2 * Pi * Mi * M)) * exp( -Qe * V(a) / (Kb * Ti)))}
Ge a b VALUE = { IF ( V(a) >= V(b),  Qe * Ne * A * sqrt( (Kb * Te / (2 * Pix * Me))) * (1 + (Qe * V(a) / (Kb * Te)))^ebeta,
+  Qe * Ne * A * sqrt( Kb * Te / (2 * Pi * Me)) * exp( Qe * V(a) / (Kb * Te)))}
R a b 10000000T
.ends
.END
